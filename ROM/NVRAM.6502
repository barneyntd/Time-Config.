OS_RomTable			= &02A1
KeyCodeR			= &B3
EconetIDreg			= &FE18

OSB_KbdScan			= &79
OSB_KbdScanAll		= &7A
OSB_KeyRptDelay		= &0B
OSB_KeyRtpRate		= &0C
OSB_CapsSetting		= &CA
OSB_CapsLEDs		= &76
OSB_BellVolume		= &D4
OSB_TVAdjust		= &90
OSB_LanguageRom		= &FC
OSB_RXBaudRate		= &07
OSB_TXBaudRate		= &08
OSB_PrinterDest		= &05
OSB_PrinterIgnore	= &06
OSB_TubePresence	= &EA
OSB_SerialFormat	= &9C
OSB_SFormatMask		= %11100011
OSB_BreakType		= &FD
OSB_KbdStartOpt		= &FF
OSB_KbdBOOTMask		= %00001000

NVR_EconetStation	= 0
NVR_EconetServer	= 1
NVR_EconetServerNet	= 2
NVR_EconetPrint		= 3
NVR_EconetPrintNet	= 4
NVR_DefaultRoms		= 5
NVR_DefLangMask		= %11110000
NVR_DefFSMask		= %00001111
NVR_Roms07Status	= 6
NVR_Roms8FStatus	= 7
NVR_VDUSettings		= 10
NVR_ModeMask		= %00000111
NVR_InterlaceMask	= %00010000
NVR_VDUAdjustMask	= %11100000
NVR_CapsSettings	= 11
NVR_CapsSMask		= %00001000
NVR_CapsNMask		= %00010000
NVR_CapsLMask		= %00100000
NVR_KeyRptDelay		= 12
NVR_KeyRptRate		= 13
NVR_PrinterIgnore	= 14
NVR_TubeSerialPrint	= 15
NVR_TubeMask		= %00000001
NVR_SerialBaudMask	= %00011100
NVR_PrinterDestMask	= %11100000
NVR_BellSFormat		= 16
NVR_BellLoudMask	= %00000010
NVR_BOOTMask		= %00010000
NVR_SFormatMask		= %11100000


\*******************************************************\
\														\
\				Service call 01: startup				\
\														\
\*******************************************************\

.NVR_resetTable
	EQUB 0					\\ Econet station number (overwritten by jumpers)
	EQUB &FE				\\ Econet file server
	EQUB 0
	EQUB &EB				\\ Econet print server
	EQUB 0
	EQUB &FF				\\ Default filing system & language
	EQUB &FF				\\ Roms 0-7 inserted
	EQUB &FF				\\ Roms 8-F inserted
	EQUB 0					\\ Reserved for EDIT
	EQUB 0					\\ Reserved for Telecom
	EQUB &07				\\ *TV and MODE (overwritten by keyboard switches)
	EQUB &20				\\ ADFS default drive, ADFS load dir, CAPS, FDRIVE
	EQUB &32				\\ keyboard repeat delay
	EQUB &08				\\ keyboard repeat rate
	EQUB &0A				\\ printer ignore character
	EQUB &39				\\ Printer destination, Baud rate, Tube
	EQUB &A2				\\ Serial format, BOOT, Bell loudness
.NVR_resetTableEnd
	
.NVR_setVariables
	TYA
	PHA
	LDA #OSB_BreakType
	LDX #0
	LDY #&FF
	JSR OSBYTE				\\ look up break type
	TXA
	BNE hardBreak
.softBreak
	PLA
	TAY	
	RTS
.hardBreak
	LDX #KeyCodeR
	LDA #OSB_KbdScan
	JSR OSBYTE				\\ check for R key pressed
	TXA
	BPL noReset
	LDX #NVR_resetTableEnd - NVR_resetTable - 1
	CLC
.resetLoop
	LDY NVR_resetTable,X
	JSR FRAM_writeByte
	DEX
	BPL resetLoop
	LDA #OSB_KbdStartOpt
	LDX #0
	LDY #&FF
	JSR OSBYTE				\\ read keyboard startup options
	TXA
	PHA
	AND #NVR_ModeMask		\\ screen mode
	TAY
	LDX #NVR_VDUSettings
	CLC
	JSR FRAM_writeByte
	PLA						\\ retrieve startup options
	ASL A
	AND #NVR_BOOTMask
	EOR #NVR_BOOTMask
	ORA #&A2				\\ Serial format, BOOT, Bell loudness
	TAY
	LDX #NVR_BellSFormat
	CLC
	JSR FRAM_writeByte
	LDY EconetIDreg			\\ read econet ID jumpers
	LDX #NVR_EconetStation
	JSR FRAM_writeByte

.noReset
	CLC
	LDX #NVR_Roms07Status
	JSR FRAM_readByte		\\	read *unplug for roms 0-7
	STY TempSpace
	LDX #NVR_Roms8FStatus
	JSR FRAM_readByte		\\	read *unplug for roms 8-F
	STY TempSpace+1
	LDA #0
	LDX #&07
.unplugLoop
	ROL TempSpace
	BCS dontUnplug1
	STA OS_RomTable,X		\\ unplug rom
.dontUnplug1
	ROL TempSpace+1
	BCS dontUnplug2
	STA OS_RomTable+8,X		\\ unplug rom
.dontUnplug2
	DEX
	BPL unplugLoop
	LDX OSROMNum			\\ current rom number
	LDA &8006				\\ rom type
	STA OS_RomTable,X		\\ reinsert self
	
	LDX #NVR_VDUSettings
	CLC
	JSR FRAM_readByte		\\ read *configure TV
	LDA #&16
	JSR OSWRCH				\\ set screen mode
	TYA
	PHA
	AND #NVR_ModeMask
	JSR OSWRCH				\\ set screen mode
	TAX
	LDY #LO(NOT(NVR_ModeMask))
	LDA #&FF
	JSR OSBYTE				\\ store in startup options
	PLA
	LSR A
	LSR A
	LSR A
	LSR A
	LSR A
	TAX
	ROL A
	AND #1
	TAY
	LDA #OSB_TVAdjust
	JSR OSBYTE				\\ set *TV
	
	LDX #NVR_CapsSettings
	CLC
	JSR FRAM_readByte		\\ read *configure CAPS
	TYA
	AND #NVR_CapsSMask
	BEQ notSC
	LDX #%10100000			\\ SHIFT CAPS
	BNE setCaps
.notSC
	TYA
	AND #NVR_CapsNMask
	BEQ notNolock
	LDX #%00110000			\\ No shift or caps
	BEQ setCaps
.notNolock
	LDX #%00100000			\\ CAPS
.setCaps
	LDY #0
	LDA #OSB_CapsSetting	\\ set caps mode
	JSR OSBYTE
	LDA #OSB_CapsLEDs		\\ set LEDs
	JSR OSBYTE
	
	LDX #NVR_KeyRptDelay
	CLC
	JSR FRAM_readByte		\\ read *configure Repeat delay
	TYA
	TAX
	LDY #0
	LDA #OSB_KeyRptDelay
	JSR OSBYTE
	
	LDX #NVR_KeyRptRate
	CLC
	JSR FRAM_readByte		\\ read *configure Repeat rate
	TYA
	TAX
	LDY #0
	LDA #OSB_KeyRtpRate
	JSR OSBYTE
	
	LDX #NVR_PrinterIgnore
	CLC
	JSR FRAM_readByte		\\ read *configure Repeat rate
	TYA
	TAX
	LDY #0
	LDA #OSB_PrinterIgnore
	JSR OSBYTE
	
	LDX #NVR_TubeSerialPrint
	CLC
	JSR FRAM_readByte		\\ read *configure Tube, baud rate & printer
	TYA
	LSR A
	LSR A
	PHA
	AND #%00000111
	PHA
	TAX
	INX
	LDY #0
	LDA #OSB_RXBaudRate
	JSR OSBYTE				\\ set receive baud rate
	PLA
	TAX
	INX
	LDA #OSB_TXBaudRate
	JSR OSBYTE				\\ set transmit baud rate
	PLA
	LSR A
	LSR A
	LSR A
	TAX
	LDY #0
	LDA #OSB_PrinterDest	\\ set printer destination
	JSR OSBYTE

	LDX #NVR_BellSFormat
	CLC
	JSR FRAM_readByte		\\ read *configure Bell loudness, BOOT & serial format
	TYA
	PHA
	AND #NVR_BellLoudMask
	BNE loudBell
	LDX #(256-7*8)
	LDY #0
	LDA #OSB_BellVolume
	JSR OSBYTE				\\ set bell volume to 4
.loudBell
	PLA
	LSR A
	PHA
	AND #OSB_KbdBOOTMask
	EOR #OSB_KbdBOOTMask
	TAX
	LDY #LO(NOT(OSB_KbdBOOTMask))
	LDA #OSB_KbdStartOpt
	JSR OSBYTE				\\ change startup options BOOT bit to NVRAM version
	PLA
	LSR A
	LSR A
	AND #LO(NOT(OSB_SFormatMask))
	TAX
	LDY #OSB_SFormatMask
	LDA #OSB_SerialFormat
	JSR OSBYTE				\\ set serial format
	JMP softBreak

\*******************************************************\
\														\
\				Service call FF: tube					\
\														\
\*******************************************************\

.NVR_TubeControl
	TYA
	PHA
	LDX #NVR_TubeSerialPrint
	CLC
	JSR FRAM_readByte
	TYA
	AND #NVR_TubeMask
	BNE notKillTube
	STA OSROMNum			\\ A is zero; block tube initialisation
.notKillTube
	PLA
	TAY
	LDA #&FF
	RTS

\*******************************************************\
\														\
\				Service call 03: filing system			\
\														\
\*******************************************************\

.NVR_defaultFS
	TYA
	PHA
	LDA #OSB_KbdScanAll
	JSR OSBYTE
	CPX #&FF
	BNE noDefaultFS			\\ key pressed, so ignore default
	LDX #NVR_DefaultRoms
	CLC
	JSR FRAM_readByte		\\ read default roms
	TYA
	AND #NVR_DefFSMask		\\ default file system rom
	CMP OSROMNum			\\ compare with this
	BCS noDefaultFS			\\ bigger numbers are already tried; don't try again
	ADC #1					\\ it's about to be decremented
	STA OSROMNum			\\ skip to default rom
.noDefaultFS
	PLA
	TAY
	LDA #3
	RTS


	
	

